plugins {
    id("com.android.library")
    id("kotlin-android")
    id("com.github.dcendents.android-maven")
}
group = 'com.github.FPhoenixCorneaE'

android {
    compileSdkVersion Deps.android.compileSdkVersion
    buildToolsVersion Deps.android.buildToolsVersion

    defaultConfig {
        minSdkVersion Deps.android.minSdkVersion
        targetSdkVersion Deps.android.targetSdkVersion
        versionCode Deps.android.versionCode
        versionName Deps.android.versionName

        consumerProguardFiles 'consumer-rules.pro'
    }

    buildTypes {
        release {
            // 执行proguard混淆
            minifyEnabled false
            // Zipalign优化
            zipAlignEnabled false
            // 移除无用的resource文件
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt')
            proguardFiles 'proguard-rules-universal.pro'
            proguardFiles 'proguard-rules.pro'
        }
        debug {
            // 执行proguard混淆
            minifyEnabled false
            // Zipalign优化
            zipAlignEnabled false
            // 移除无用的resource文件
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt')
            proguardFiles 'proguard-rules-universal.pro'
            proguardFiles 'proguard-rules.pro'
        }
    }

    compileOptions {
        targetCompatibility JavaVersion.VERSION_1_8
        sourceCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    dexOptions {
        jumboMode true
    }

    lintOptions {
        abortOnError false
    }

    configurations.all {
        resolutionStrategy {
            force(Deps.kotlin.kotlinStdlib)
        }
    }
}

dependencies {
    compileOnly fileTree(dir: 'libs', include: ['*.jar'])
    compileOnly Deps.supportLibs
    compileOnly Deps.kotlinLibs
    compileOnly Deps.rxJava2Libs
    compileOnly Deps.navigationLibs
    compileOnly Deps.rxPermissions
    compileOnly Deps.coilLibs
    compileOnly Deps.gson
    compileOnly Deps.eventBus
    compileOnly Deps.logger
}

//添加以下配置，否则上传后的jar包看不到注释-------------------------------------------------------------

// 指定编码
tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}
// 打包源码
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}
task javadoc(type: Javadoc) {
    failOnError false
    source = android.sourceSets.main.java.sourceFiles
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    classpath += configurations.compile
}
// 制作文档(Javadoc)
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives sourcesJar
    archives javadocJar
}